if empty(glob('~/.vim/autoload/plug.vim'))
  silent !curl -fLo ~/.vim/autoload/plug.vim --create-dirs
    \ https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('~/.vim/plugged')
Plug 'VundleVim/Vundle.vim'

Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
" Plug 'tpope/vim-dispatch'
" Plug 'tpope/fugitive'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-unimpaired'

Plug 'luochen1990/rainbow'
Plug 'easymotion/vim-easymotion'
Plug 'frioux/vim-regedit'
Plug 'mileszs/ack.vim'

" autocomplete/linter/templates
Plug 'Valloric/YouCompleteMe'
Plug 'rdnetto/YCM-Generator', { 'branch': 'stable' }
Plug 'SirVer/ultisnips'
Plug 'honza/vim-snippets'
Plug 'noahfrederick/vim-skeleton'
Plug 'chiel92/vim-autoformat'
Plug 'vim-syntastic/syntastic'

" colorscheme
Plug 'lithammer/vim-eighties'
Plug 'ldelossa/vimdark'
Plug 'altercation/vim-colors-solarized'
Plug 'morhetz/gruvbox'
Plug 'aonemd/kuroi.vim'

" python
" disable this for now, use hardcoded version in the dotfiles
" Plug 'psf/black', { 'tag': '19.10b0' }

" LaTeX
Plug 'vim-latex/vim-latex'

" HTML/XML
Plug 'mattn/emmet-vim'

" suan/vim-instant-markdown

call plug#end()            " required

" basic miscellaneous
set encoding=utf-8

set viewoptions=folds,options,cursor,unix,slash " Better Unix / Windows compatibility
set virtualedit=onemore             " Allow for cursor beyond last character
set history=1000                    " Store a ton of history (default is 20)
" set spell                           " Spell checking on
set hidden                          " Allow buffer switching without saving

set gdefault                    " Substitute all words on a line, :s///g gets only the first
set showmatch                   " Show matching brackets/parenthesis
set matchtime=3                 " for 3 ms
set incsearch                   " Find as you type search
set hlsearch                    " Highlight search terms
set ignorecase                  " Case insensitive search
set smartcase                   " Case sensitive when uppercase present
set wildmenu                    " Show list instead of just completing
set wildmode=list:longest,full  " Command <Tab> completion, list matches, then longest common part, then all.
set wildignore=*.pyc,*.class

set nojoinspaces                " Prevents inserting two spaces after punctuation on a join (J)
set splitright                  " Puts new vsplit windows to the right of the current
set splitbelow                  " Puts new split windows to the bottom of the current

set scrolloff=8
set listchars=tab:>-,trail:~,extends:>,precedes:<,space:·,eol:¬
set number
set relativenumber
set numberwidth=3
set cursorline				" highlight current line
" set cursorcolumn
set showmode

set backspace=indent,eol,start
set shiftwidth=4                " Use indents of 4 spaces
set expandtab                   " Tabs are spaces, not tabs
set tabstop=4                   " An indentation every four columns
set softtabstop=4               " Let backspace delete indent

set mouse=a

" proper highlight for .tex
let g:tex_flavor = "latex"

" optics
set linespace=0
set background=dark

if has('gui_running')  " in gvim do this
    set termguicolors
else
    set t_Co=256
endif

" colorscheme kuroi
" colorscheme antares
" colorscheme gruvbox
colorscheme gruvbox-bootleg

" let g:solarized_termcolors=256  " needed for terminal use
" colorscheme solarized

" Setting up the directories
set directory=~/.myvim/swap
set backup                      " Backups are nice ...
set backupdir=~/.myvim/backup
if has('persistent_undo')
    set undofile                " So is persistent undo ...
    set undolevels=1000         " Maximum number of changes that can be undone
    set undoreload=10000        " Maximum number lines to save for undo on a buffer reload
    set undodir=~/.myvim/undo
endif


if has('clipboard')
    if has('unnamedplus')  " When possible use + register for copy-paste
        set clipboard=unnamed,unnamedplus
    else         " On mac and Windows, use * register for copy-paste
        set clipboard=unnamed
    endif
endif

if has('cmdline_info')
    set laststatus=2            " always show statusline

    set statusline=             " reset upon vimrc reload
    set statusline+=%<          " cut at start if the line is too long
    set statusline+=%f          " path relative to current directory
    set statusline+=\           " space
    set statusline+=%h          " help buffer flag
    set statusline+=%m          " modified flag
    set statusline+=%r          " readonly flag
    set statusline+=%=          " start right align
    set statusline+=\:b%n       " buffer number with :b in front
    set statusline+=%y          " filetype
    set statusline+=%w          " preview window flag
    set statusline+=\           " space
    set statusline+=%l,%c       " current line and column
    set statusline+=%V          " Virtual column number as -{num}. Not displayed if equal to 'c'
    set statusline+=\           " space
    set statusline+=%P          " percentage through file

    set showcmd                 " Show partial commands in status line and
                                " Selected characters/lines in visual mode
endif

" Instead of reverting the cursor to the last position in the buffer, we
" set it to the first line when editing a git commit message
au FileType gitcommit au! BufEnter COMMIT_EDITMSG call setpos('.', [0, 1, 1, 0])

" mappings and shortcuts
inoremap jk <ESC>
inoremap kj <ESC>

" move properly when lines are wrapped
nnoremap j gj
nnoremap k gk
" relative jump precise with count if there are wrapped lines
nnoremap gj j
nnoremap gk k

" Yank from the cursor to the end of the line, to be consistent with C and D.
nnoremap Y y$

" when in visual mode, search for selected text
vnoremap * ""y/<C-r>"<CR>
vnoremap # ""y?<C-r>"<CR>

" when in visual mode, J is usually a typo from holding shift for line visual
vnoremap J j
vnoremap K k

let mapleader = "\<Space>"

"   #########   "
"   Leadermap
"   #########   "

" toggle things
" clear highlight
nnoremap <silent> <leader>nh :set hlsearch!<CR>
nnoremap <silent> <leader>nc :set cursorcolumn!<CR>
nnoremap <silent> <leader>nw :set wrap!<CR>
nnoremap <silent> <leader>nl :set list!<CR>
nnoremap <leader>np :set paste!<CR>
nnoremap <leader>nr :RainbowToggle<CR>
nnoremap <leader>ns :setlocal spell! spelllang=en_us<CR>

" quick save and quit
nnoremap <leader>w :update<CR>
nnoremap <leader>q :q<CR>
" edit and source .vimrc
nnoremap <leader>re :tabedit $MYVIMRC<CR>
nnoremap <leader>rs :source $MYVIMRC<CR>
" yank whole file
nnoremap <leader>ya myggyG`y

" split screen
nnoremap <leader>d <C-w>v
nnoremap <leader>s <C-w>s
" navigate windows
nnoremap <leader>h <C-w>h
nnoremap <leader>j <C-w>j
nnoremap <leader>k <C-w>k
nnoremap <leader>l <C-w>l
" move windows around
nnoremap <leader>mh <C-w><S-h>
nnoremap <leader>mj <C-w><S-j>
nnoremap <leader>mk <C-w><S-k>
nnoremap <leader>ml <C-w><S-l>
" terminal navigation
tnoremap <leader><leader>h <C-w>h
tnoremap <leader><leader>j <C-w>j
tnoremap <leader><leader>k <C-w>k
tnoremap <leader><leader>l <C-w>l
" move terminal around
tnoremap <leader><leader>mh <C-w><S-h>
tnoremap <leader><leader>mj <C-w><S-j>
tnoremap <leader><leader>mk <C-w><S-k>
tnoremap <leader><leader>ml <C-w><S-l>

" make sessions and restore them
" you can also start vim with a session 'vim -S ~/mysession.vim'
let g:sessions_dir = '~/.vim/sessions'
exec 'nnoremap <Leader>ms :mksession! ' . g:sessions_dir . '/<C-D>'
exec 'nnoremap <Leader>mr :source ' . g:sessions_dir. '/<C-D>'

"   #######   "
"   Plugins   "
"   #######   "

" black virtualenv outside the dotfiles
let g:black_virtualenv = '~/.myvim/black'

" yank and comment the current line with commentary
nnoremap gcy :normal yygcc<CR>
nnoremap gcp :normal yygccp<CR>

let g:rainbow_active = 1 " toggle with :RainbowToggle

" YouCompleteMe
let g:ycm_global_ycm_extra_conf = '~/.vim/.ycm_extra_conf.py'
let g:ycm_complete_in_comments = 1
let g:ycm_complete_in_strings = 1
let g:ycm_seed_identifiers_with_syntax = 1 " autocomplete with keywords for the language
let g:ycm_min_num_of_chars_for_completion = 1
" Apply YCM FixIt
map <F9> :YcmCompleter FixIt<CR>

" Autoformat, override this in after/ftplugin for type specific formatter
nnoremap <leader>bb :Autoformat<CR>

" UltiSnips
let g:UltiSnipsExpandTrigger = "<c-j>"
let g:UltiSnipsJumpForwardTrigger  = "<c-j>"
let g:UltiSnipsJumpBackwardTrigger = "<c-p>"
" open snippet file in separate window
let g:UltiSnipsEditSplit = "horizontal"
" edit snippets
nmap <leader>ue :UltiSnipsEdit<cr>

" syntastic
let g:syntastic_python_checkers = ['mypy', 'flake8']

set statusline+=\           " space
set statusline+=%#warningmsg#
set statusline+=%{SyntasticStatuslineFlag()}
set statusline+=%*
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 1
let g:syntastic_check_on_open = 1
let g:syntastic_check_on_wq = 0

" TODO toggle this https://developer.ibm.com/articles/l-vim-script-1/
" flake8 args
" line length errors [E501]
" too many # at start of comment [E266]
" whitespace before ':' [E203]
" line break before binary operator [W503]
" local variable 'var_name' is assigned to but never used [F841]
let g:syntastic_python_flake8_args = "--ignore=E501,E266,E203,W503"
" let g:syntastic_python_mypy_args = "--ignore-missing-imports"

" emmet
" use <c-y>, for now, put this in ftplugin if you really need it
" let g:user_emmet_leader_key=','

" if there is only one element in location list call :lfirst instead of :lnext
nmap <silent> [l  <Plug>LocationPrevious
nmap <silent> ]l  <Plug>LocationNext
" if there is only one element in quickfix list call :cfirst instead of :cnext
nmap <silent> [q  <Plug>QuickFixPrevious
nmap <silent> ]q  <Plug>QuickFixNext

" FZF
set runtimepath+=~/.fzf
nnoremap <leader>fs :FZF ~/snippet/<CR>
nnoremap <leader>fd :FZF ~/dotfiles/<CR>

" ack.vim
if executable('ag')
  let g:ackprg = 'ag --vimgrep'
endif
nnoremap <Leader>a :LAckAdd!<Space>

" the function is in autoload/strip_trailing_whitespace.vim
nnoremap <F5> :call strip_trailing_whitespace#StripTrailingWhitespace()<CR>

" http://vim.wikia.com/wiki/Restore_cursor_to_file_position_in_previous_editing_session
" Restore cursor to file position in previous editing session
function! ResCur()
    if line("'\"") <= line("$")
        silent! normal! g`"
        return 1
    endif
endfunction
augroup resCur
    autocmd!
    autocmd BufWinEnter * call ResCur()
augroup END

" https://github.com/vim/vim/issues/2490#issuecomment-393973253
" scroll inside terminal
function! ExitNormalMode()
    unmap <buffer> <silent> <RightMouse>
    vertical resize -5
    call feedkeys("a")
endfunction

function! EnterNormalMode()
    if &buftype == 'terminal' && mode('') == 't'
        call feedkeys("\<c-w>N")
        call feedkeys("\<c-y>")
        map <buffer> <silent> <RightMouse> :call ExitNormalMode()<CR>
    endif
    vertical resize +5
endfunction

tmap <silent> <ScrollWheelUp> <c-w>:call EnterNormalMode()<CR>

if filereadable(expand('~/.vimrc.local'))
    source ~/.vimrc.local
endif
